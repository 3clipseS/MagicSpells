plugins {
    id("java-library")
    id("org.jetbrains.kotlin.jvm") version "1.6.0"
    id("io.papermc.paperweight.userdev") version "1.3.5"
}

dependencies {
    paperweightDevelopmentBundle(group: "io.papermc.paper", name: "dev-bundle", version: "1.18.2-R0.1-SNAPSHOT")

    shadow(group: "org.apache.commons", name: "commons-math3", version: "3.6.1")
    shadow(group: "com.github.elBukkit", name: "EffectLib", version: "master-SNAPSHOT")
    shadow(group: "co.aikar", name: "acf-paper", version: "0.5.0-SNAPSHOT")
    shadow(group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.6.10")

    implementation(group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.7.0") { transitive = false }
    implementation(group: "com.github.libraryaddict", name: "LibsDisguises", version: "v10.0.25") { transitive = false }
    implementation(group: "com.github.MilkBowl", name: "VaultAPI", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.PlaceholderAPI", name: "PlaceholderAPI", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.TechFortress", name: "GriefPrevention", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.InventivetalentDev", name: "GlowAPI", version: "1.5.1-SNAPSHOT") { transitive = false }

    implementation(group: "com.github.EngineHub", name: "WorldGuard", version: "master") {
        exclude(module: "worldguard-libs")
    }
    implementation(group: "com.github.EngineHub", name: "WorldEdit", version: "master") {
        transitive = false
        exclude(module: "ap")
        exclude(module: "cli")
        exclude(module: "fabric")
        exclude(module: "forge")
        exclude(module: "sponge")
        exclude(module: "worldedit-cli")
        exclude(module: "worldedit-sponge")
        exclude(module: "worldedit-forge-mc1.16.1")
        exclude(module: "worldedit-fabric-mc1.16.1")
    }

    //implementation("com.github.Updated-NoCheatPlus.NoCheatPlus:ncpbuildbase:master-SNAPSHOT")
}

shadowJar {
    setArchivesBaseName("MagicSpells")
    relocate("org.apache.commons.math3", "com.nisovin.magicspells.shaded.org.apache.commons")
    relocate("de.slikey.effectlib", "com.nisovin.magicspells.shaded.effectlib")
    relocate("ru.xezard.glow", "com.nisovin.magicspells.shaded.xglow")
    relocate("kotlin", "com.nisovin.magicspells.shaded.kotlin")
    relocate("co.aikar.commands", "com.nisovin.magicspells.shaded.acf")
    relocate("co.aikar.locales", "com.nisovin.magicspells.shaded.locales")
    archiveClassifier.set("")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
        javaParameters = true
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "com.nisovin.magicspells"
            from components.java
        }
    }
}

tasks {
    build {
        dependsOn(reobfJar)
    }
}
